module StellarHeliosHelpers

import {
    tx, 
    get_current_input,
    get_current_validator_hash
} from ScriptContext

// keep this as-is.  Make RealnumSettingsValueV2 or something else if it needs to change
struct RealnumSettingsValueV1 {
    name: String
    microInt: Int // "Real" semantics, times 1_000_000
}

func didSign(a: Address) -> Bool {
    pkh : PubKeyHash = a.credential.switch{
        PubKey{h} => h,
        Validator => error("trustee can't be a contract")
        // _ => error("trustee can't be a contract")
    };
    // print("checking if trustee signed: " + pkh.show());

    tx.is_signed_by(pkh)
}

func TODO(task: String) -> () {
    // ðŸŸ¥  ðŸ˜³ðŸ’¦ red checkbox, face, sweat droplets
    print("  ðŸŸ¥  ðŸ˜³ðŸ’¦  TODO: " + task + "\n")
}

func TRACE(id: ByteArray, message: String) -> () {
    // todo: how to prevent the id from being optimized out?
    print(id.decode_utf8_safe());
    print(message)
}

func REQT(reqt: String, assertion : Bool=true) -> () {
    // â—red exclamation mark
    print("â— "+ reqt);
    assert(assertion, "  âŒâŒ ^ failed: " + reqt)
}
func bREQT(reqt: String, assertion : Bool=true) -> Bool {
    REQT(reqt, assertion);
    true
}

//! represents the indicated token name as a Value
func mkTv(
    mph: MintingPolicyHash, 
    tn: String="", 
    tnBytes: ByteArray=tn.encode_utf8(),
    count : Int = 1
) -> Value {
    assert(tnBytes.length > 0, "missing reqd tn or tnBytes");
    Value::new(
        AssetClass::new(mph, tnBytes), 
        count
    )
}

//! returns the charter-token from our minter, as a Value
func tvCharter(mph: MintingPolicyHash)  -> Value {
    mkTv(mph, "charter")
}

func returnsValueToScript(value : Value) -> Bool {
    input : TxInput = get_current_input();
    input.value.contains(value) &&
    tx.outputs.any( (txo : TxOutput) -> Bool {
        txo.address == input.address &&
        txo.value.contains(value)
    } )
}


func getOutputWithValue(v : Value) -> TxOutput {
    tx.outputs.find((txo: TxOutput) -> { txo.value >= v })
}

struct outputAndDatum[T] {
    output: TxOutput
    datum: T
    rawData: Data
}

func getSingleAssetValue(input: TxInput) -> Value{
    inputMap : Map[MintingPolicyHash]Map[ByteArray]Int = input.value.get_assets().to_map();
    assert( inputMap.length == 1, 
        "multiple assets"
        // "getSingleAssetValue needs single-asset input"
    );

    inputTokens : Map[ByteArray]Int = inputMap.head_value;
    assert(inputTokens.length == 1, 
        "multiple tokens"
        // "getSingleAssetValue needs single-token input"
    );

    input.value.get_assets()
}

// func outputDatum[T](newTxo : TxOutput) -> T {
//     T::from_data(newTxo.datum.inline)
// }

func getOutputForInput(input: TxInput) -> TxOutput {
    inputValue : Value = getSingleAssetValue(input);

    getOutputWithValue(inputValue)
}

//! retrieves the redeemer for a specific input
func mustFindInputRedeemer(
    txInput: TxInput    
) -> Data {
    targetId : TxOutputId = txInput.output_id;
    redeemers : Map[ScriptPurpose]Data = tx.redeemers;
    spendsExpectedInput : ScriptPurpose = redeemers.find_key( 
        (purpose : ScriptPurpose) -> { purpose.switch{ 
            sp: Spending => {
                // print ("oid: " + sp.output_id.show());
                sp.output_id == targetId
            }, 
            _ => false 
        } }
    );
    redeemers.get(spendsExpectedInput)
}

// XXX this doesn't work because using a field of this type
//   causes the compiler to believe there would be two enum wrappers,
//   one for this Solo variant, and another for the T type.
// Instead, the off-chain code needs to construct the wrapper,
// but the field needs to pretend there is no wrapper.

// enum NestedTaggedStruct[T] {
//     Solo{ thing: T }

//     func unwrap(self) -> T {
//         print("NestedTaggedStruct: unwrap");

//         self.switch {
//             Solo{ thing } => {
//                 print("NestedTaggedStruct: unwrapped thing");
//                 thing
//             },
//             _ => error("NestedTaggedStruct: unwrap: unknown variant")
//         }
//     }
// }
 
// field-names style of struct, arbitrary & extensible
// field list, can be interpreted by any script that defines a 
// field-names style of struct with its own fields & data types.
struct AnyData {
    id: ByteArray "@id"  // same as the UUT name for this data
    type: String "tpe"

    // can have other fields; receiver will interpret their target types.
}

func fromCip68Wrapper(value: Data) -> Data {
    value.switch {
        // IntData, ByteArrayData, MapData, ListData and ConstrData
        ConstrData{_anyIndex, fields} => {
            fields.head.switch {
                MapData => fields.head,
                _ => error("bad cast from ConstrData{_, ...fields} to mStruct: first field must be a map")
            }
        },
        _ => error("bad cast to mStruct from non-ConstrData")
    }
}

func getTimeRange(granularity: Duration = Duration::HOUR) -> TimeRange {
    validity : TimeRange = tx.time_range
    assert(
        validity.end - validity.start 
            <= granularity,
        "txn duration must be at most "+granularity.show() + "ms"
    );
    validity
}

func startsExactlyAt(
    tr: TimeRange,
    t: Time
) -> Bool{
    t == tr.start
}

// use [optional: tcx.futureData() and] txn.validFor(), then tcx.txnEndTime()
// to synchronize an off-chain variable with the validity.end seen here.
func endsExactlyAt(
    tr: TimeRange,
    t: Time
) -> Bool{
    t == tr.end
}

func startsAfter(
    tr: TimeRange,
    t : Time
) -> Bool{
    tr.start > t
}

func endsBefore(
    tr: TimeRange,
    t : Time
) -> Bool{
    tr.end < t
}

func now(granularity: Duration = Duration::HOUR) -> Time {
    validity : TimeRange = getTimeRange(granularity);
    validity.start
}        

